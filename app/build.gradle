apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "nl.testchamber.mailordercoffeeshop"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
        dataBinding {
            enabled = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.includeAndroidResources = true
    }
    flavorDimensions "providerVersion", "dataType"


    productFlavors {
        simpledata {
            dimension "providerVersion"
            versionNameSuffix "-simpledata"
            isDefault.set(true)
        }
        retrofitdata {
            dimension "providerVersion"
            versionNameSuffix "-retrofitdata"
        }
        local {
            dimension "dataType"
            versionNameSuffix "-local"
            isDefault.set(true)
        }
        remote {
            dimension "dataType"
            versionNameSuffix "-remote"
        }
    }
    sourceSets {
        main { java.srcDirs = ['src/main/java'] }
        test { java.srcDirs = ['src/test/java'] }
        androidTest { java.srcDirs = ['src/androidTest/java'] }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        def types = variant.buildType.name
        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (names.contains("retrofitdata") && names.contains("local") || types.contains("release")) {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }
}

dependencies {
    implementation project(':apiservice')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.media:media:$support_version"
    implementation "com.google.android.material:material:1.1.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "com.github.JakeWharton:ViewPagerIndicator:$view_pager_indicator_version"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"

    testImplementation 'junit:junit:4.13'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test.ext:truth:1.2.0'
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation group: 'androidx.test.espresso', name: 'espresso-contrib', version: '3.3.0-beta01'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.3.0-beta01'
    androidTestImplementation group: 'androidx.test.espresso', name: 'espresso-contrib', version: '3.3.0-beta01'

    debugImplementation('androidx.fragment:fragment-testing:1.2.4') {
        exclude group: 'androidx.test', module: 'core'
    }
    androidTestUtil 'androidx.test:orchestrator:1.2.0'
}
repositories {
    mavenCentral()
}
